(ns kotoba.user-test.mcp
  "Stdio MCP adapter for the pure user-test contracts. No browser credential or
  participant record is held by this process; callers pass EDN-shaped JSON and
  hosts keep their own evidence store."
  (:require [clojure.data.json :as json]
            [clojure.string :as str]
            [kotoba.user-test :as user-test]
            [mcp.execute :as execute]
            [mcp.model :as model]
            [mcp.ports :as ports])
  (:import [java.io BufferedReader]))

(def tools
  [{:name "user_test_validate"
    :description "Validate a portable user-test study."
    :schema {:type "object" :properties {"study" {:type "object"}}
             :required ["study"]}}
   {:name "user_test_plan"
    :description "Compile a study into an executor-neutral journey plan."
    :schema {:type "object" :properties {"study" {:type "object"}}
             :required ["study"]}}
   {:name "user_test_evaluate"
    :description "Score immutable observed outcomes and return issue-shaped findings."
    :schema {:type "object"
             :properties {"study" {:type "object"} "run" {:type "object"}}
             :required ["study" "run"]}}
   {:name "user_test_project_summary"
    :description "Aggregate human, synthetic and recipe runs without conflating them."
    :schema {:type "object"
             :properties {"project" {:type "string"}
                          "evaluations" {:type "array"}}
             :required ["project" "evaluations"]}}])

(def manifest
  (reduce (fn [m {:keys [name description schema]}]
            (model/add-tool m name {:description description
                                    :input-schema schema}))
          (model/server "kotoba-user-test" "1")
          tools))

(defn invoke [tool-name args]
  (let [args (user-test/from-wire args)
        study (:study args)]
    (case tool-name
      "user_test_validate"
      {:ok? (user-test/valid-study? study)
       :errors (user-test/study-errors study)}
      "user_test_plan" (user-test/execution-plan study)
      "user_test_evaluate"
      (let [evaluation (user-test/evaluate study (:run args))]
        {:evaluation evaluation :findings (user-test/findings evaluation)})
      "user_test_project_summary"
      (user-test/project-summary (:project args) (:evaluations args))
      {:error (str "unknown tool: " tool-name) :type "mcp/unknown-tool"})))

(defn ports []
  {:tool (reify ports/ITool
           (invoke [_ tool-name args]
             (try (invoke tool-name args)
                  (catch clojure.lang.ExceptionInfo error
                    {:error (.getMessage error)
                     :type (some-> (:type (ex-data error)) str
                                   (str/replace #"^:" ""))}))))})

(defn respond [request]
  (when (contains? request "id")
    (execute/handle (ports) manifest request)))

(defn serve! [^BufferedReader reader writer]
  (loop []
    (when-let [line (.readLine reader)]
      (when-not (str/blank? line)
        (if-let [request (try (json/read-str line) (catch Exception _ nil))]
          (when-let [response (respond request)]
            (.write writer (str (json/write-str response) "\n"))
            (.flush writer))
          (do (.write writer
                      (str (json/write-str
                            {"jsonrpc" "2.0" "id" nil
                             "error" {"code" -32700 "message" "parse error"}})
                           "\n"))
              (.flush writer))))
      (recur))))

(defn -main [& _]
  (serve! (BufferedReader. (java.io.InputStreamReader. System/in "UTF-8"))
          (java.io.OutputStreamWriter. System/out "UTF-8")))
